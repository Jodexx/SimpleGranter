plugins {
    id 'java'
    id 'com.gradleup.shadow' version '8.3.0'
}

group = 'com.jodexindustries'
version = '1.0.5'

def targetJavaVersion = 8

java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion

    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

repositories {
    mavenCentral()
    maven { url = 'https://jitpack.io' }
    maven {
        name = 'papermc-repo'
        url = uri('https://repo.papermc.io/repository/maven-public/')
    }
    maven {
        name = 'sonatype'
        url = uri('https://oss.sonatype.org/content/groups/public/')
    }
    maven { url = 'https://repo.alessiodp.com/releases/' }
}

dependencies {
    compileOnly 'com.destroystokyo.paper:paper-api:1.12.2-R0.1-SNAPSHOT'
    compileOnly 'com.github.MilkBowl:VaultAPI:1.7'
    compileOnly 'com.j256.ormlite:ormlite-jdbc:6.1'
    compileOnly 'net.luckperms:api:5.4'
    implementation 'net.byteflux:libby-bukkit:1.3.0'
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    def props = [version: version]
    inputs.properties(props)
    filteringCharset = 'UTF-8'
    filesMatching('plugin.yml') {
        expand(props)
    }
}

tasks.jar.enabled = false

shadowJar {
    archiveClassifier.set('')

    relocate("net.byteflux", "com.jodexindustries")
}

build {
    dependsOn shadowJar
}
